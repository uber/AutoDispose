/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
  resolutionStrategy {
    eachPlugin {
      switch (requested.id.id) {
        case 'org.jetbrains.dokka':
          useModule('org.jetbrains.dokka:dokka-gradle-plugin:0.9.16-eap-3')
          break
        case 'org.jetbrains.dokka-android':
          useModule('org.jetbrains.dokka:dokka-android-gradle-plugin:0.9.16-eap-3')
          break
        case 'com.android.application':
          useModule('com.android.tools.build:gradle:2.3.0')
          break
        case 'com.android.library':
          useModule('com.android.tools.build:gradle:2.3.0')
          break
        case 'org.jetbrains.kotlin.jvm':
          useModule('org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.21')
          break
        case 'org.jetbrains.kotlin.android':
          useModule('org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.21')
          break
      }
    }
  }
  repositories {
    jcenter()
    google()
    maven {
      url 'https://dl.bintray.com/kotlin/kotlin-eap'
    }
    gradlePluginPortal()
  }
}

rootProject.name = 'autodispose-root'
if (System.getenv("ANDROID_HOME") != null) {
  include ':android:autodispose-android'
  include ':android:autodispose-android-kotlin'
  include ':android:autodispose-android-archcomponents'
  include ':android:autodispose-android-archcomponents-kotlin'
  include ':android:autodispose-android-archcomponents-test'
  include ':android:autodispose-android-archcomponents-test-kotlin'
  include ':sample'
}
include ':autodispose'
include ':autodispose-kotlin'
include 'autodispose-rxlifecycle'
include ':test-utils'
include ':static-analysis:autodispose-error-prone-checker'


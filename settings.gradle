/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
  apply from: file('gradle/dependencies.gradle')
  resolutionStrategy {
    eachPlugin {
      switch (requested.id.id) {
        case 'org.jetbrains.dokka':
          useModule("org.jetbrains.dokka:dokka-gradle-plugin:${deps.versions.dokka}")
          break
        case 'org.jetbrains.dokka-android':
          useModule("org.jetbrains.dokka:dokka-android-gradle-plugin:${deps.versions.dokka}")
          break
        case 'com.android.application':
        case 'com.android.library':
        case 'com.android.lint':
          useModule(deps.build.gradlePlugins.android)
          break
        case 'org.jetbrains.kotlin.jvm':
          useModule(deps.build.gradlePlugins.kotlin)
          break
        case 'org.jetbrains.kotlin.android':
          useModule(deps.build.gradlePlugins.kotlin)
          break
        case 'net.ltgt.errorprone':
          useVersion(deps.versions.errorPronePlugin)
          break
      }
    }
  }
  repositories {
    jcenter()
    google()
    gradlePluginPortal()
  }
}

rootProject.name = 'autodispose-root'
if (System.getenv("ANDROID_HOME") != null) {
  include ':android:autodispose-android'
  include ':android:autodispose-android-ktx'
  include ':android:autodispose-android-archcomponents'
  include ':android:autodispose-android-archcomponents-ktx'
  include ':android:autodispose-android-archcomponents-test'
  include ':android:autodispose-android-archcomponents-test-ktx'
  include ':sample'
}
include ':autodispose'
include ':autodispose-ktx'
include ':lifecycle:autodispose-lifecycle'
include ':lifecycle:autodispose-lifecycle-jdk8'
include ':lifecycle:autodispose-lifecycle-ktx'
include 'autodispose-rxlifecycle'
include ':test-utils'
include ':static-analysis:autodispose-error-prone'
include ':static-analysis:autodispose-lint'

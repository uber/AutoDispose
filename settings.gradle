/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

pluginManagement {
  apply from: file('gradle/dependencies.gradle')
  resolutionStrategy {
    eachPlugin {
      String id = requested.id.id
      if (id.startsWith("com.android")) {
        useModule(deps.build.gradlePlugins.android)
      } else if (id.startsWith("org.jetbrains.kotlin")) {
        useModule(deps.build.gradlePlugins.kotlin)
      } else {
        switch (id) {
          case 'org.jetbrains.dokka':
            useModule("org.jetbrains.dokka:dokka-gradle-plugin:${deps.versions.dokka}")
            break
          case 'org.jetbrains.dokka-android':
            useModule("org.jetbrains.dokka:dokka-android-gradle-plugin:${deps.versions.dokka}")
            break
          case 'net.ltgt.errorprone':
            useVersion(deps.versions.errorPronePlugin)
            break
          case 'net.ltgt.nullaway':
            useVersion(deps.versions.nullawayPlugin)
            break
          case 'ru.vyarus.animalsniffer':
            useVersion(deps.versions.animalSniffer)
            break
        }
      }
    }
  }
  repositories {
    mavenCentral()
    google()
    gradlePluginPortal()
    jcenter()
  }
}

rootProject.name = 'autodispose-root'
if (System.getenv("ANDROID_HOME") != null) {
  include ':android:autodispose-android'
  include ':android:autodispose-androidx-lifecycle'
  include ':android:autodispose-androidx-lifecycle-test'
  include ':sample'
}
include ':autodispose'
include ':autodispose-coroutines-interop'
include ':autodispose-lifecycle'
include ':test-utils'
include ':static-analysis:autodispose-error-prone'
include ':static-analysis:autodispose-lint'

/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
  id 'java-library'
  id 'net.ltgt.errorprone'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
  compileOnly deps.apt.autoService
  compileOnly deps.build.errorProneCheckApi

  testImplementation(deps.build.errorProneTestHelpers) {
    exclude group: "junit", module: "junit"
  }
  testImplementation deps.rx.java
  testImplementation deps.test.junit
  testImplementation project(':autodispose')
  testImplementation project(':lifecycle:autodispose-lifecycle')

  errorproneJavac deps.build.errorProneJavac
  errorprone deps.build.nullAway
  errorprone deps.build.errorProne
}

tasks.withType(JavaCompile).configureEach {
  options.errorprone {
    option("NullAway:AnnotatedPackages", "com.uber")
    check("NullAway", CheckSeverity.ERROR)
  }
}

test {
  jvmArgs "-Xbootclasspath/p:${configurations.errorproneJavac.asPath}"
}

apply from: rootProject.file("gradle/gradle-mvn-push.gradle")

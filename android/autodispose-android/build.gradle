/*
 * Copyright (C) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
import net.ltgt.gradle.errorprone.CheckSeverity

plugins {
  id 'com.android.library'
  id 'net.ltgt.errorprone'
}

android {
  compileSdkVersion deps.build.compileSdkVersion
  buildToolsVersion deps.build.buildToolsVersion

  defaultConfig {
    minSdkVersion deps.build.minSdkVersion
    targetSdkVersion deps.build.targetSdkVersion
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility deps.build.javaVersion
    targetCompatibility deps.build.javaVersion
  }
  testOptions {
    execution 'ANDROIDX_TEST_ORCHESTRATOR'
  }
}

dependencies {
  api project(':autodispose')
  api deps.rx.java
  api deps.androidx.annotations
  implementation deps.rx.android

  errorproneJavac deps.build.errorProneJavac
  errorprone deps.build.nullAway
  errorprone deps.build.errorProne

  lintChecks project(':static-analysis:autodispose-lint')

  testImplementation project(':test-utils')
  testImplementation deps.test.junit
  testImplementation deps.test.truth
  androidTestImplementation project(':test-utils')
  androidTestImplementation deps.androidx.annotations
  androidTestImplementation(deps.test.androidExtJunit) {
    // https://issuetracker.google.com/issues/117486311
    exclude group: "org.junit"
  }
  androidTestImplementation deps.test.androidRunner
  androidTestImplementation deps.test.androidRules
  androidTestUtil deps.test.androidOrchestrator
}

afterEvaluate {
  tasks.withType(JavaCompile).configureEach {
    options.errorprone {
      option("NullAway:AnnotatedPackages", "com.uber")
      check("NullAway", CheckSeverity.ERROR)
      check("RestrictTo", CheckSeverity.OFF)
    }
  }
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

/*
 * Copyright (c) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'com.android.library'
  id 'net.ltgt.errorprone'
}

android {
  compileSdkVersion deps.build.compileSdkVersion
  buildToolsVersion deps.build.buildToolsVersion

  defaultConfig {
    minSdkVersion deps.build.minSdkVersion
    targetSdkVersion deps.build.targetSdkVersion
    consumerProguardFiles 'consumer-proguard-rules.txt'
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    testApplicationId "com.uber.autodispose.android.lifecycle.androidTest"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
  lintOptions {
    lintConfig file('lint.xml')
  }
  testOptions {
    execution 'ANDROID_TEST_ORCHESTRATOR'
  }
}

dependencies {
  annotationProcessor deps.build.nullAway
  testAnnotationProcessor deps.build.nullAway
  androidTestAnnotationProcessor deps.build.nullAway
  annotationProcessor deps.support.arch.lifecycle.compiler

  api project(':autodispose')
  api project(':lifecycle:autodispose-lifecycle')
  api deps.support.annotations
  api deps.support.arch.lifecycle.runtime

  implementation project(':android:autodispose-android')
  implementation deps.rx.android

  errorprone deps.build.errorProne

  androidTestImplementation project(':android:autodispose-android-archcomponents-test')
  androidTestImplementation project(':test-utils')
  androidTestImplementation deps.test.androidRunner
  androidTestImplementation deps.test.androidRules
  androidTestUtil deps.test.androidOrchestrator
}

tasks.withType(JavaCompile) {
  options.compilerArgs += ["-Xep:NullAway:ERROR",
                           "-XepOpt:NullAway:AnnotatedPackages=com.uber",
                           "-Xep:RestrictTo:OFF"]
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

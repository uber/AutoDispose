/*
 * Copyright (c) 2017. Uber Technologies
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url deps.build.repositories.plugins }
  }

  dependencies {
    classpath deps.build.gradlePlugins.android
    classpath deps.build.gradlePlugins.errorProne
  }
}

apply plugin: 'com.android.library'
apply plugin: "net.ltgt.errorprone"

android {
  compileSdkVersion deps.build.compileSdkVersion
  buildToolsVersion deps.build.buildToolsVersion

  defaultConfig {
    minSdkVersion deps.build.minSdkVersion
    targetSdkVersion deps.build.targetSdkVersion
    consumerProguardFiles 'consumer-proguard-rules.txt'
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    testApplicationId "com.uber.autodispose.android.lifecycle.androidTest"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }
}

dependencies {
  annotationProcessor deps.build.nullAway
  testAnnotationProcessor deps.build.nullAway
  annotationProcessor deps.support.arch.lifecycle.compiler

  compile project(':android:autodispose-android')
  compile deps.support.arch.lifecycle.common
  compile deps.support.arch.lifecycle.runtime

  provided deps.misc.errorProneAnnotations
  provided deps.misc.javaxExtras

  errorprone deps.build.checkerFramework
  errorprone deps.build.errorProne

  androidTestCompile project(':android:autodispose-android-archcomponents-test')
  androidTestCompile project(':test-utils')
  androidTestCompile deps.test.androidRunner
  androidTestCompile deps.test.androidRules
}

tasks.withType(JavaCompile) {
  options.compilerArgs += ["-Xep:NullAway:ERROR",
                           "-XepOpt:NullAway:AnnotatedPackages=com.uber"]
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
